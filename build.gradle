import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'signing'
    id 'eclipse'
    id 'jacoco'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id 'net.nemerosa.versioning' version '3.0.0'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'net.reini'

defaultTasks 'build'

configurations {
    asciidoclet
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api:4.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
    testImplementation 'org.easymock:easymock:5.1.0'
    asciidoclet 'org.asciidoctor:asciidoclet:1.+'
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  withJavadocJar()
  withSourcesJar()
}

test {
  useJUnitPlatform()
}

signing {
    required { project.hasProperty('signing.password') && !project.version.toString().endsWith('-SNAPSHOT') }
    sign publishing.publications
}

jar {
    manifest {
        attributes 'Implementation-Title': 'SLF4J CDI injection providers',
                   'Implementation-Version': project.version,
                   'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
                   'Build-Revision': versioning.info.commit,
                   'Automatic-Module-Name': "org.slf4j.cdi"
        metaInf {
            from file('.')
            include 'LICENSE'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'SLF4J CDI injection providers'
                description = 'Provides partial backwards compatibility classes in order to more easier migrate to a newer JUnit version.'
                url = 'https://github.com/reinhapa/slf4j-cdi'
                scm {
                    connection = 'scm:git:git@github.com:reinhapa/slf4j-cdi.git'
                    developerConnection = 'scm:git:git@github.com:reinhapa/slf4j-cdi.git'
                    url = 'https://github.com/reinhapa/slf4j-cdi.git'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'reinhapa'
                        name = 'Patrick Reinhart'
                        email = 'patrick@reini.net'
                    }
                }
            }
        }
    }
    repositories {
        if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = version.toString().endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}
